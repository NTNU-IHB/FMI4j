plugins {
    id "base"
    id "org.jetbrains.kotlin.jvm" version "1.5.10" apply false
}

println "Gradle version is ${gradle.getGradleVersion()}"

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.7'
}

group = "info.laht.fmi4j"
version = rootProject.file("VERSION").readLines()[0]
println("Building ${rootProject.name} v$version")

subprojects { sub ->

    group = rootProject.group
    version = rootProject.version

    pluginManager.withPlugin('java') {

        repositories {
            mavenCentral()
        }

        pluginManager.withPlugin('kotlin') {

            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
            }

            compileKotlin {
                kotlinOptions {
                    jvmTarget = "1.8"
                    javaParameters = true
                    freeCompilerArgs = ["-Xjvm-default=enable"]
                }
            }
            compileTestKotlin {
                kotlinOptions.jvmTarget = "1.8"
            }

        }

    }

}

tasks.create('cleanTemp') {

    doLast {
        def temp = new File(System.getProperty("java.io.tmpdir"))
        temp.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return dir == temp && name.startsWith("fmi4j_")
            }
        }).each {
            it.deleteDir()
        }
    }

}

clean.dependsOn(cleanTemp)
